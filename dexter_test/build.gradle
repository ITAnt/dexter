import com.android.dx.command.dexer.Main

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath 'com.google.android.tools:dx:1.7'
    }
}

apply plugin: 'java'
sourceCompatibility = targetCompatibility = 1.6

ext.jarFile = new File(libsDir, project.name + ".jar")
ext.dexFile = new File(libsDir, "classes.dex")
ext.apkFile = new File(libsDir, project.name + "_apk.jar")

task dex (dependsOn: jar) {

    inputs.file project.ext.jarFile
    outputs.file project.ext.dexFile

    doLast {
        Main.main([ "--output", dexFile.getPath(), jarFile.getPath() ] as String[])
    }
}

task apk (type: Jar, dependsOn: dex) {
    baseName = project.name + "_apk"
    from dexFile
}
